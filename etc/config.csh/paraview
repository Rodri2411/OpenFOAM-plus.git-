#----------------------------------*-sh-*--------------------------------------
# =========                 |
# \\      /  F ield         | OpenFOAM: The Open Source CFD Toolbox
#  \\    /   O peration     |
#   \\  /    A nd           | Copyright (C) 2011-2016 OpenFOAM Foundation
#    \\/     M anipulation  | Copyright (C) 2016-2017 OpenCFD Ltd.
#------------------------------------------------------------------------------
# License
#     This file is part of OpenFOAM, licensed under GNU General Public License
#     <http://www.gnu.org/licenses/>.
#
# File
#     config.csh/paraview
#
# Description
#     Setup for PARAVIEW (partially cmake, qt too)
#     Sourced from OpenFOAM-<VERSION>/etc/cshrc or from foamPV alias
#
#     For system-wide cmake:            cmake_version=cmake-system
#     For system-wide qt:               ParaView_QT=qt-system
#
# Note
#     The following env. variables are required for building plugins:
#         ParaView_DIR
#         ParaView_INCLUDE_DIR
#         PV_PLUGIN_PATH
#
#     For central installations not located under ThirdParty, the environment
#     values must be set manually. For example,
#
#         setenv ParaView_DIR /opt/paraview/paraview-5.4.1
#         setenv ParaView_INCLUDE_DIR $ParaView_DIR/include/paraview-5.4
#         setenv PV_PLUGIN_PATH $FOAM_LIBBIN/paraview-5.4
#
#         setenv PATH ${ParaView_DIR}/bin:${PATH}
#         setenv LD_LIBRARY_PATH ${ParaView_DIR}/lib/paraview-5.4:${LD_LIBRARY_PATH}
#         unsetenv ParaView_VERSION     # avoid using ThirdParty settings
#
#------------------------------------------------------------------------------
# USER EDITABLE PART: Changes made here may be lost with the next upgrade

setenv ParaView_VERSION 5.4.1
set    ParaView_QT=qt-system
set cmake_version=cmake-system

# END OF (NORMAL) USER EDITABLE PART
#------------------------------------------------------------------------------
set pv_api=auto         # Normally auto or pair of digits (eg, '5.4' etc)

if (! $?ParaView_DIR ) setenv ParaView_DIR
if (! $?WM_COMPILER_LIB_ARCH ) setenv WM_COMPILER_LIB_ARCH

# Compiler-specific location for ThirdParty installations
set archDir="$WM_THIRD_PARTY_DIR/platforms/$WM_ARCH$WM_COMPILER"

# Clean PATH and LD_LIBRARY_PATH
set cleaned=`$WM_PROJECT_DIR/bin/foamCleanPath "$PATH" "$ParaView_DIR $archDir/cmake- $archDir/qt- $archDir/ParaView-"`
if ( $status == 0 ) setenv PATH $cleaned

if ( $?LD_LIBRARY_PATH ) then
    set cleaned=`$WM_PROJECT_DIR/bin/foamCleanPath "$LD_LIBRARY_PATH" "$ParaView_DIR $archDir/qt- $archDir/ParaView-"`
    if ( $status == 0 ) setenv LD_LIBRARY_PATH $cleaned
endif

# ThirdParty cmake
set cmake=$archDir/$cmake_version
if ( -r $cmake/bin/cmake ) then
    # _foamAddPath not available when foamPV alias is used
    setenv PATH $cmake/bin:${PATH}
endif

# Evaluate command-line parameters for ParaView
while ( $#argv > 0 )
    switch ($argv[1])
    case ParaView*=*:
        # name=value  -> setenv name value
        eval "setenv $argv[1]:s/=/ /"
        breaksw
    endsw
    shift
end


# Require that ParaView_VERSION has not been unset.
# Avoids conflict with an alternative (non-ThirdParty) installation.
if ( $?ParaView_VERSION ) then

    # Set API to correspond to VERSION
    # pv_api is <digits>.<digits> from ParaView_VERSION
    if (! $?pv_api ) set pv_api=auto
    switch ("$ParaView_VERSION")
    case "$pv_api".*:
        # API and VERSION appear to correspond
        breaksw

    case [0-9]*:
        # Extract API from VERSION
        set pv_api=`echo ${ParaView_VERSION} | \
            sed -e 's/^\([0-9][0-9]*\.[0-9][0-9]*\).*$/\1/'`
        breaksw
    endsw

    setenv ParaView_DIR $archDir/ParaView-$ParaView_VERSION
    set pvSubDir=paraview-$pv_api

    # Set paths if binaries are present
    if ( -r $ParaView_DIR ) then
        set pvLibDir=${ParaView_DIR}/lib/$pvSubDir
        set pvPython=$ParaView_DIR/Utilities/VTKPythonWrapping

        setenv PATH ${ParaView_DIR}/bin:${PATH}
        setenv ParaView_INCLUDE_DIR $ParaView_DIR/include/$pvSubDir
        setenv PV_PLUGIN_PATH $FOAM_LIBBIN/$pvSubDir

        # Add in qt libraries as required
        set qtDir="$archDir/$ParaView_QT"
        if ( -d "$qtDir" ) then
            foreach qtLibDir ("$qtDir/lib$WM_COMPILER_LIB_ARCH" "$qtDir/lib")
                if ( -d "$qtLibDir" ) then
                    setenv LD_LIBRARY_PATH "${qtLibDir}:${LD_LIBRARY_PATH}"
                    break
                endif
            end
        endif

        # Add in python libraries if required
        if ( -r $pvPython ) then
            if ($?PYTHONPATH) then
                setenv PYTHONPATH ${PYTHONPATH}:${pvPython}:$pvLibDir
            else
                setenv PYTHONPATH ${pvPython}:$pvLibDir
            endif
        endif

        # Prepend paraview libraries
        setenv LD_LIBRARY_PATH "${pvLibDir}:${LD_LIBRARY_PATH}"

        if ($?FOAM_VERBOSE && $?prompt) then
            echo "Using paraview"
            echo "    ParaView_DIR         : $ParaView_DIR"
            echo "    ParaView_INCLUDE_DIR : $ParaView_INCLUDE_DIR"
            echo "    library dir          : $pvLibDir"
            echo "    PV_PLUGIN_PATH       : $PV_PLUGIN_PATH"
        endif
    else
        if ($?FOAM_VERBOSE && $?prompt) then
            echo "No paraview found"
            echo "    ParaView_DIR         : $ParaView_DIR"
        endif

        unsetenv ParaView_INCLUDE_DIR PV_PLUGIN_PATH
        setenv ParaView_DIR   # Defined but empty (used by foamPV alias)
    endif

endif

unset cleaned archDir
unset cmake cmake_version
unset pv_api pvSubDir pvLibDir pvPython qtDir qtLibDir
unsetenv ParaView_VERSION ParaView_QT

#------------------------------------------------------------------------------
